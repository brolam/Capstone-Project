apply plugin: 'com.android.application'

repositories {
    mavenLocal()
    flatDir {
        dirs 'libs'
    }
    maven {
        url 'https://maven.fabric.io/public'
    }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "br.com.brolam.cloudvision"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }
}

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if ((details.requested.group == 'com.google.android.gms') || ((details.requested.group == 'com.google.firebase'))) {
                details.useVersion "10.0.1"
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.1.1'
    compile 'com.android.support:design:25.1.1'
    testCompile 'junit:junit:4.12'

    /*
     Mobile Vision API https://developers.google.com/vision/introduction
     Escanear blocos de texto nas imagens capturadas através da câmera fotográfica
     do smartphone ou tablet.
    */
    compile 'com.google.android.gms:play-services-vision'

    /*
    Firebase Authentication https://firebase.google.com/docs/auth/
     Realizar a autenticação do aplicativo em uma conta de usuário da Google ou E-mail.
     */
    compile 'com.google.firebase:firebase-auth'

    /*
    Firebase Realtime Database https://firebase.google.com/docs/database/
    Armazenar os Notes Vision e blocos de texto.
     */
    compile 'com.google.firebase:firebase-database'
    /*
    Firebase Storage https://firebase.google.com/docs/storage/
    Armazenar as imagens de plano de fundo dos Notes Vision.
     */
    compile 'com.google.firebase:firebase-storage'

    /*
    FirebaseUI for Android https://github.com/firebase/FirebaseUI-Android
    Reutilizar UIs na integração com o Firebase.
     */
    compile 'com.firebaseui:firebase-ui:1.1.1'

}





